#!ipxe

# Microsoft Windows
# https://www.microsoft.com

set win_base_url http://netbootxyz.aleixohome.lan
set memdisk ${win_base_url}/win/files/memdisk
set win_folder win10_22h2_english
set win_arch x64
set win_folder win10_22h2_english
goto ${menu} ||

:windows
set os Microsoft Windows
clear win_version
menu ${os} 
item --gap Installers
item win_install ${space} Load ${os} Installer...
item --gap Options:
item kind_set ${space} Install/Tool [ ${win_folder} ]
item arch_set ${space} Architecture [ ${win_arch} ]
item url_set ${space} Base URL [ ${win_base_url} ]
choose win_version || goto windows_exit
goto ${win_version}

:kind_set
iseq ${win_folder} win10_22h2_english && set win_folder HBCD_PE_x64 || set win_folder win10_22h2_english
goto windows

:arch_set
iseq ${win_arch} x64 && set win_arch x86 || set win_arch x64
goto windows

:url_set
echo Set the HTTP URL of an extracted Windows ISO without the trailing slash:
echo e.g. http://www.mydomain.com/windows
echo
echo -n URL: ${} && read win_base_url
echo
echo netboot.xyz will attempt to load the following files:
echo kernel ${win_base_url}/win/files/wimboot
echo initrd ${win_base_url}/win/configs/auto.bat auto.bat
echo initrd ${win_base_url}/win/configs/winpeshl.ini winpeshl.ini
echo ${win_base_url}/win/${win_arch}/${win_folder}/bootmgr
echo ${win_base_url}/win/${win_arch}/${win_folder}/bootmgr.efi
echo ${win_base_url}/win/${win_arch}/${win_folder}/boot/bcd
echo ${win_base_url}/win/${win_arch}/${win_folder}/boot/boot.sdi
echo ${win_base_url}/win/${win_arch}/${win_folder}/sources/boot.wim
echo
prompt Press any key to return to Windows Menu...
goto windows

:win_install
isset ${win_base_url} && goto boot || echo URL not set... && goto url_set

:boot
imgfree
kernel ${win_base_url}/win/files/wimboot
initrd ${win_base_url}/win/configs/auto.bat auto.bat
initrd ${win_base_url}/win/configs/winpeshl.ini winpeshl.ini
initrd -n bootmgr     ${win_base_url}/win/${win_arch}/${win_folder}/bootmgr       bootmgr ||
initrd -n bootmgr.efi ${win_base_url}/win/${win_arch}/${win_folder}/bootmgr.efi   bootmgr.efi ||      
initrd -n bcd         ${win_base_url}/win/${win_arch}/${win_folder}/boot/bcd      bcd ||
initrd -n bcd         ${win_base_url}/win/${win_arch}/${win_folder}/Boot/BCD      bcd ||
initrd -n boot.sdi    ${win_base_url}/win/${win_arch}/${win_folder}/boot/boot.sdi boot.sdi ||
initrd -n boot.sdi    ${win_base_url}/win/${win_arch}/${win_folder}/Boot/boot.sdi boot.sdi ||
initrd -n boot.wim    ${win_base_url}/win/${win_arch}/${win_folder}/sources/boot.wim boot.wim
boot

:windows_exit
exit 0
